buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'info.solidsoft.gradle:cdeliveryboy:0.3.0'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.4.0'    //Workaround on Axion compatibility issues with Gradle 3.0
        classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.8.0'  //auto-drop-after-release
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.7'
    }
}

apply plugin: 'groovy'
apply plugin: 'info.solidsoft.cdeliveryboy'

cDeliveryBoy {
    releaseBranch = "cDeliveryBoy"
    autoPromote = true
}

group = 'io.gitlab.nexus-at'

project.version = scmVersion.version

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile 'junit:junit:4.12'
}

nexusStaging {
    packageGroup = "io.gitlab.nexus-at"
}

nexus {
     sign = !project.hasProperty("skipSigning")
}

modifyPom {
    project {
        name 'Dummy CD plugin'
        description 'Dummy Gradle plugin for testing Continuous Delivery with CDeliveryBoy plugin'
        url 'https://github.com/szpak/dummy-cd-plugin/'
        inceptionYear '2017'

        scm {
            url 'https://github.com/szpak/dummy-cd-plugin/'
            connection 'scm:https://github.com/szpak/dummy-cd-plugin.git'
            developerConnection 'scm:git://github.com/szpak/dummy-cd-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'szpak'
                name 'Marcin ZajÄ…czkowski'
                email 'mszpak ATT wp DOTT pl'
                roles {
                    role 'developer'
                    role 'despot'
                }
            }
        }
    }
}

uploadArchives.dependsOn { check }

if (hasProperty('pluginPortal')) {

    apply plugin: 'com.gradle.plugin-publish'

    pluginBundle {
        website = 'https://github.com/szpak/dummy-cd-plugin/'
        vcsUrl = 'https://github.com/szpak/dummy-cd-plugin/'

        plugins {
            plugin {
                id = 'io.gitlab.nexus-at.dummy-cd'
                displayName = 'dummy-cd-plugin'
                description = 'Dummy Gradle plugin for testing Continuous Delivery with CDeliveryBoy plugin'
                tags = ['dummy', 'fake']
            }
        }
    }
}
