apply plugin: 'info.solidsoft.cdeliveryboy'
apply plugin: 'io.codearte.nexus-upload-staging'    //https://github.com/Codearte/gradle-nexus-staging-plugin/issues/76

cDeliveryBoy {
    git {
        releaseBranch = "cDeliveryBoy"
        addPoweredByMessage = false
    }
    tasks {
        uploadArchivesTask = "uploadArchivesStaging"   //to use "nexus-upload-staging"
        promoteRepositoryTask = "releaseRepository"
    }
    nexus {
        autoPromote = true
    }
}

scmVersion {
    versionIncrementer('incrementPatch')
    hooks {
        pre 'fileUpdate', [file       : 'README.md', pattern: { v, p -> /"io\.gitlab\.nexus-at:dummy-cd-plugin:.*"/ },
                           replacement: { v, p -> "\"io.gitlab.nexus-at:dummy-cd-plugin:$v\"" }]
    }
}

project.group = 'io.gitlab.nexus-at'
project.version = scmVersion.version

nexusStaging {
    packageGroup = "io.gitlab.nexus-at"
}

nexus {
     sign = !project.hasProperty("skipSigning")
}

modifyPom {
    project {
        name 'Dummy CD plugin'
        description 'Dummy Gradle plugin for testing Continuous Delivery with CDeliveryBoy plugin'
        url 'https://github.com/szpak/dummy-cd-plugin/'
        inceptionYear '2017'

        scm {
            url 'https://github.com/szpak/dummy-cd-plugin/'
            connection 'scm:https://github.com/szpak/dummy-cd-plugin.git'
            developerConnection 'scm:git://github.com/szpak/dummy-cd-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'szpak'
                name 'Marcin ZajÄ…czkowski'
                email 'mszpak ATT wp DOTT pl'
                roles {
                    role 'developer'
                    role 'despot'
                }
            }
        }
    }
}

uploadArchives.dependsOn { check }

if (hasProperty('pluginPortal')) {

    apply plugin: 'com.gradle.plugin-publish'

    pluginBundle {
        website = 'https://github.com/szpak/dummy-cd-plugin/'
        vcsUrl = 'https://github.com/szpak/dummy-cd-plugin/'

        plugins {
            plugin {
                id = 'io.gitlab.nexus-at.dummy-cd'
                displayName = 'dummy-cd-plugin'
                description = 'Dummy Gradle plugin for testing Continuous Delivery with CDeliveryBoy plugin'
                tags = ['dummy', 'fake']
            }
            plugin2 {
                id = 'io.gitlab.nexus-at.dummy-cd2'
                displayName = 'dummy-cd-plugin2'
                description = 'Dummy Gradle plugin2 for testing Continuous Delivery with CDeliveryBoy plugin'
                tags = ['dummy', 'fake']
            }
        }
    }

    publishPlugins {
        onlyIf { ciBuild.inReleaseMode }
        mustRunAfter releaseRepository
    }
    ciBuild.dependsOn publishPlugins
}

if (hasProperty('changelogSync')) {

    task syncChangelog(type: Exec) {
        doFirst { logger.info("Synchronizing changelog with GitHub for version ${project.version}") }
        commandLine 'chandler', 'push', "release/${project.version}", '--tag-prefix=release/'
    }

    syncChangelog {
        onlyIf { ciBuild.inReleaseMode }
        mustRunAfter releaseRepository
        if (project.tasks.findByName("publishPlugins")) {
            mustRunAfter publishPlugins
        }
    }
    ciBuild.dependsOn syncChangelog
}

//Some workarounds on CDeliveryBoy limitations - #3 and #13 (see below)

//Note. Referring non built-in types in external build script is problematic: https://github.com/gradle/gradle/issues/1262
//Note2. Task dependency removal is problematic due to: https://github.com/gradle/gradle/pull/6143/
["uploadArchives", "createRepository", "pointUploadArchivesToExplicitRepository"].each { String taskName ->
    tasks.named(taskName).configure {
        onlyIf {
            boolean onlyIfValue = resolveOnlyIfValueForUploadTasks()
            if (!onlyIfValue) {
                logger.lifecycle("Task disabled due to environment settings for ciBuild")
            }
            return onlyIfValue
        }
    }
}

boolean resolveOnlyIfValueForUploadTasks() {
    return !isSnapshotBuildWithSkipReleaseOnCI() && !isExtraBuildOfReleaseTagOnCI()
}
//Workaround on https://github.com/szpak/CDeliveryBoy/issues/3 - do not upload snapshots from other Java versions on CI server
boolean isSnapshotBuildWithSkipReleaseOnCI() {
    return (project.version as String).endsWith("-SNAPSHOT") && System.env.SKIP_RELEASE == "true" && isCiBuildTaskCalled()
}
//Workaround on https://github.com/szpak/CDeliveryBoy/issues/13 - do not upload artifacts on extra build on CI server (e.g. from cron) of release tag
boolean isExtraBuildOfReleaseTagOnCI() {
    return !(project.version as String).endsWith("-SNAPSHOT") && !ciBuild.inReleaseMode && isCiBuildTaskCalled()
}
boolean isCiBuildTaskCalled() {
    //It covers only basic (idiomatic) case that "ciBuild" task has been called directly. Examining taskGraph would be better, however, more problematic due to late preparation.
    return gradle.startParameter.taskNames.contains("ciBuild")
}


